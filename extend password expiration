set serveroutput on
DECLARE
user_name  varchar2(20) := '&1';
exp_date dba_users.expiry_date%TYPE;
acct_stats dba_users.account_status%TYPE;
profile_name dba_users.profile%TYPE;
profile_limit_reuse_time_store dba_profiles.LIMIT%TYPE;
profile_limit_reuse_max_store dba_profiles.LIMIT%TYPE;
profile_limit_pass_ver_func dba_profiles.LIMIT%TYPE;
/*set hashed_pwd to 300 since spare4 hash is larger than previous 20.*/
hashed_pwd  varchar2(300) ;
BEGIN
dbms_output.put_line ('RUN AS SYS or any user with access user$');
select password into hashed_pwd from user$ where name=user_name;
select profile,account_status,expiry_date into profile_name,acct_stats,exp_date from dba_users where username=user_name;
/*12cR2 hashed password is in spare4 column*/
IF hashed_pwd IS NULL THEN
 select SPARE4 into hashed_pwd from user$ where name=user_name;
END IF;
IF ( acct_stats = 'EXPIRED' OR acct_stats = 'EXPIRED(GRACE)' ) THEN
 BEGIN
 /* To remove expiration,PASSWORD_REUSE_TIME and PASSWORD_REUSE_MAX limit should be temporarily extended and alter to same password. */
  select LIMIT into profile_limit_reuse_time_store from dba_profiles where resource_name='PASSWORD_REUSE_TIME' and profile=profile_name;
  select LIMIT into profile_limit_reuse_max_store from dba_profiles where resource_name='PASSWORD_REUSE_MAX' and profile=profile_name;
  select LIMIT into profile_limit_pass_ver_func from dba_profiles where resource_name='PASSWORD_VERIFY_FUNCTION' and profile=profile_name;
  dbms_output.put_line('USERNAME: '||user_name);
  dbms_output.put_line('ACCOUNT_STATUS: '||acct_stats);
  dbms_output.put_line('ACCOUNT_EXPIRATION_DATE: '||exp_date);
  dbms_output.put_line('PASSWORD_REUSE_TIME: '||profile_limit_reuse_time_store);
  dbms_output.put_line('PASSWORD_REUSE_MAX: '||profile_limit_reuse_max_store);
  dbms_output.put_line('PASSWORD_VERIFY_FUNCTION: '||profile_limit_pass_ver_func);
  execute immediate ('alter profile '||profile_name||' limit PASSWORD_REUSE_TIME unlimited');
  execute immediate ('alter profile '||profile_name||' limit PASSWORD_REUSE_MAX unlimited');
  execute immediate ('alter profile '||profile_name||' limit PASSWORD_VERIFY_FUNCTION NULL');
  execute immediate ('alter user '||user_name||' identified by values '''||hashed_pwd||'''');
  execute immediate ('alter profile '||profile_name||' limit PASSWORD_REUSE_TIME '||' '||profile_limit_reuse_time_store);
  execute immediate ('alter profile '||profile_name||' limit PASSWORD_REUSE_MAX '||' '||profile_limit_reuse_max_store);
  IF ( profile_limit_pass_ver_func <> 'NULL') THEN
   execute immediate ('alter profile '||profile_name||' limit PASSWORD_VERIFY_FUNCTION '||profile_limit_pass_ver_func);
  END IF;
 EXCEPTION 
  WHEN OTHERS THEN dbms_output.put_line(sqlerrm); 
 END;
END IF;
select profile,account_status,expiry_date into profile_name,acct_stats,exp_date from dba_users where username=user_name;
select LIMIT into profile_limit_reuse_time_store from dba_profiles where resource_name='PASSWORD_REUSE_TIME' and profile=profile_name;
select LIMIT into profile_limit_reuse_max_store from dba_profiles where resource_name='PASSWORD_REUSE_MAX' and profile=profile_name;
select LIMIT into profile_limit_pass_ver_func from dba_profiles where resource_name='PASSWORD_VERIFY_FUNCTION' and profile=profile_name;
dbms_output.put_line('----------------------------------------');
dbms_output.put_line('USERNAME: '||user_name);
dbms_output.put_line('ACCOUNT_STATUS: '||acct_stats);
dbms_output.put_line('ACCOUNT_EXPIRATION_DATE: '||exp_date);
dbms_output.put_line('PASSWORD_REUSE_TIME: '||profile_limit_reuse_time_store);
dbms_output.put_line('PASSWORD_REUSE_MAX: '||profile_limit_reuse_max_store);
dbms_output.put_line('PASSWORD_VERIFY_FUNCTION: '||profile_limit_pass_ver_func);
END;
